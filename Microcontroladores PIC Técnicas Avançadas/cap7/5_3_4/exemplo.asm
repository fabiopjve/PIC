LIST 	P=16F627
INCLUDE <P16F627.INC>
__CONFIG _CP_OFF & _WDT_OFF & _PWRTE_OFF & _BODEN_OFF & _MCLRE_OFF & _INTRC_OSC_NOCLKOUT & _LVP_OFF
A0	EQU 0x20
A1	EQU 0x21
B0	EQU 0x22
B1	EQU 0x23
C0	EQU 0x24
C1	EQU 0x25
C2	EQU 0x26
C3	EQU 0x27
ORG	0x0000
	GOTO	INICIO
MULTIPLICA16X16:
	CLRF	C3
	CLRF	C2
	CLRF	C1
	CLRF	C0
	BSF	C1, 7
MULTL1:
	RRF	A1,F
	RRF	A0,F
	BTFSS	STATUS,C
	GOTO	MULTL2
	MOVF	B0,W
	ADDWF	C2,F
	MOVF	B1,W
	BTFSC	STATUS,C
	INCFSZ	B1,W
	ADDWF	C3,F
MULTL2:
	RRF	C3,F
	RRF	C2,F
	RRF	C1,F
	RRF	C0,F
	BTFSS	STATUS,C
	GOTO	MULTL1
	RETURN
INICIO:
	MOVLW	0xF1	
	MOVWF	A0			; A0= 0xF1	
	CLRF	A1			; A1= 0x00
	MOVLW	0x05			; 
	MOVWF	B0			; B0= 0x05
	MOVLW	0x1B
	MOVWF	B1			; B1=0x1B
	CALL	MULTIPLICA16X16		; chamada da sub-rotina de multiplicação de 
					; 16 bits
	SLEEP				; fim do programa, CPU em modo SLEEP
END
; resultado: C3=0, C2=0x19, C1=0x6F e C0=0xB5. Z=’0’, DC=’1’ e C=’1’
; AxB=C=> 0x00F1 x 0x1B05 = 0x00196FB5


